<!DOCTYPE html>
<html>
	<head>
		<title>Galaga</title>
		<link href="../../nav.css" rel="stylesheet" type="text/css">
		<link href="galaga.css" rel="stylesheet" type="text/css">
		<link rel="shortcut icon" type="image/x-icon" href="/favicon.ico">
		<script src="https://cdnjs.cloudflare.com/ajax/libs/processing.js/1.6.0/processing.min.js"></script>
	</head>
	<body>
		<nav>
			<ul>
				<li><a href="../../index.html">Home</a></li>
				<li><a href="#">Guides</a>
					<ul>
						<li><a href="../../pages/creating-your-first-game.html">Making A Game</a></li>
					</ul>
				</li>
				<li><a href="#">Games</a>
					<ul>
						<li><a href="../../games/snake.html">Snake</a></li>
						<li><a href="../../games/galaga/galaga.html">Galaga</a></li>
					</ul>
				</li>
				<li><a href="#">Apps</a>
					<ul>
						<li><a href="https://play.google.com/store/apps/details?id=xyz.crispycabot.hold_the_touch">Hold The Touch</a></li>
					</ul>
				</li>
			</ul>
		</nav>
		<br/>
		<br/>
		<br/>
		<script type="application/processing">
			PImage ship;
PVector loc;
ArrayList<Shot> shotList = new ArrayList<Shot>();
ArrayList<Star> starList = new ArrayList<Star>();
ArrayList<Enemy> enemyList = new ArrayList<Enemy>();
boolean up, right, left = false;
int shotDelay = 0;
int alienAmt = 3;
boolean increasedDiff = true;
int lives = 3;
int score = 0;
PImage heart;
final int alienWidth = 50;
final int heartWidth = 50;
boolean playing = false;
Button play;
/* @pjs preload="alien.png,spaceship.png,heart.png"; crisp="true"; */

void setup() {
  background(0);
  int[] playColor1 = {0, 0, 155};
  int[] playColor2 = {255, 255, 255};
  play = new Button(width/2, height/2, 200, 100, "Play", playColor1, playColor2);
  size(1200, 600);
  loc = new PVector(width/2, height-150);
  String url = "https://i.gyazo.com/031ce30a46282ad629f298a1864cdd96.png";
  ship = loadImage(url, "png");
  url = "https://i.gyazo.com/df7bf901c47324cef847ebafac92a936.png";
  heart = loadImage(url, "png");
  frameRate(60);
}

void draw() {
  if (playing) {
    background(0);
    starStuff();
    moveShip();
    if (up) {
      if (shotDelay == 0) {
        makeShot();
        shotDelay = 5;
      }
      else {
        shotDelay--;
      }
    }
    enemyStuff();
    enemyShotThings();
    heartStuff();
    if (lives == 0) {
      gameOver();
    }
    increaseDiff();
    fill(153, 0, 0);
    textAlign(CENTER); //Score update
    textSize(30);
    text("Score: "+score, width-100, height-50);
    image(ship, loc.x, loc.y);
  }
  else {
    play.update();
    if (play.clicked()) {
      playing = true;
      initialValues();
    }
  }
}

void keyPressed() {
  if (keyCode == RIGHT) {
    right = true;
  }
  if (keyCode == LEFT) {
    left = true;
  }
  if (keyCode == UP) {
    up = true;
  }
}

void keyReleased() {
  if (keyCode == RIGHT) {
    right = false;
  }
  if (keyCode == LEFT) {
    left = false;
  }
  if (keyCode == UP) {
    up = false;
  }
}

void increaseDiff() {
  if (score % 10 == 0) {
    if (!(increasedDiff)) {
      alienAmt++;
      increasedDiff = true;
    }
  }
  else {
    increasedDiff = false;
  }
}

void gameOver() {
  playing=false;
  textAlign(CENTER);
  textSize(60);
  fill(255,0,0);
  text("Game Over", width/2, height/2-100);
}

void initialValues() {
  shotList = new ArrayList<Shot>();
  starList = new ArrayList<Star>();
  enemyList = new ArrayList<Enemy>();
  up = false;
  right = false;
  left = false;
  shotDelay = 0;
  alienAmt = 3;
  increasedDiff = true;
  lives = 3;
  score = 0;
  loc = new PVector(width/2, height-150);
  for (int i=0; i<100; i++) { //Creates original stars
    Star temp = new Star(int(random(0, width)), int(random(0, height)));
    starList.add(temp);
  }
  for (int i=0; i<alienAmt; i++) { //Creates original enemies
    Enemy temp = new Enemy(int(random(0, int(width/alienWidth)))*alienWidth, -20);
    enemyList.add(temp);
  }
  image(ship, loc.x, loc.y); //Score text
  fill(153, 0, 0);
  textAlign(CENTER);
  textSize(20);
  text("Score: "+score, width-100, height-50);
}

void heartStuff() {
  if (lives >= 1) {
    image(heart, 5, height-50);
  }
  if (lives >= 2) {
    image(heart, 10+heartWidth, height-50);
  }
  if (lives >= 3) {
    image(heart, 15+heartWidth*2, height-50);
  }
}

void enemyShotThings() { //Various small tasks that I didn't want taking up space in draw
  for (int i=0; i < shotList.size(); i++) {
    Shot shot = shotList.get(i);
    if (shot.shotStuff()) {
      shotList.remove(shot);
    }
    for (int t=0; t<enemyList.size();t++) { //Checks if shot hit an alien
      Enemy enemy = enemyList.get(t);
      if (shot.x < enemy.x+alienWidth && shot.x > enemy.x && shot.y < enemy.y+50) {
        enemyList.remove(enemy);
        shotList.remove(shot);
        score++;
      }
    }
  }
}

void enemyStuff() {
  for (int t=0; t<enemyList.size();t++) {
    if (enemyList.get(t).updateEnemy()) {
      lives--;
      enemyList.remove(enemyList.get(t));
    }
  }
  if (enemyList.size() < alienAmt) {
    Enemy temp = new Enemy(int(random(0, int(width/120)))*120, -20);
    enemyList.add(temp);
  }
}
void starStuff() {
  for (int i=0; i < starList.size(); i++) {
    if (starList.get(i).updateStar()) {
      starList.remove(starList.get(i));
    }
  }
  if (starList.size() < 100) {
    Star temp = new Star(int(random(0, width)), 0);
    starList.add(temp);
  }
}

void makeShot() {
  Shot temp = new Shot(loc.x+36.75, loc.y+5);
  shotList.add(temp);
}

void moveShip() {
  if (right) {
    if (loc.x > width) {
      loc.x = 0;
    }
    else {
      loc.x = loc.x + 10;
    }
  }
  if (left) {
    if (loc.x < 0-50) {
      loc.x = width;
    }
    else {
      loc.x = loc.x - 10;
    }
  }
}
class Button {
  int x, y, w, h;
  String words;
  int[] buttonColor;
  int[] textColor;
  Button(int a, int b, int c, int d, String e, int[] firstColor, int[] secondColor) {
    x = a;
    y = b;
    w = c;
    h = d;
    words = e;
    buttonColor = firstColor;
    textColor = secondColor;
    
    fill(buttonColor[0], buttonColor[1], buttonColor[2]);
    rect(x, y, w, h, 10);
    fill(textColor[0], textColor[1], textColor[2]);
    textAlign(CENTER);
    textSize(50);
    text(words, x+w/2, y+h/2+5);
  }
  
  boolean clicked() {
    if (mousePressed && inButton()) {
      return true;
    }
    return false;
  }
  
  void update() {
    if (inButton()) {
      fill(buttonColor[0]-10, buttonColor[1]-10, buttonColor[2]-10);
      noStroke();
      rect(x, y, w, h, 30);
      fill(textColor[0], textColor[1], textColor[2]);
      textAlign(CENTER);
      textSize(20);
      text(words, x+w/2, y+h/2+5);
    }
    else {
      fill(buttonColor[0], buttonColor[1], buttonColor[2]);
      noStroke();
      rect(x, y, w, h, 30);
      fill(textColor[0], textColor[1], textColor[2]);
      textAlign(CENTER);
      textSize(20);
      text(words, x+w/2, y+h/2+5);
    }
  }
  
  boolean inButton() {
    if (mouseX > x && mouseX < x+w && mouseY > y && mouseY < y+h) {
      return true;
    }
    return false;
  }
}
String url = "https://i.gyazo.com/be66f04c1c801825e0a816faf2d8d8e2.png";
PImage alienShip = loadImage(url, "png");
class Enemy {
  int x, y;
  Enemy(int xx, int yy) {
    x = xx;
    y = yy;
  }
  boolean updateEnemy() {
    y = y+2;
    image(alienShip, x, y);
    if (y > height) {
      return true;
    }
    return false;
  }
}
class Shot {
  float x;
  float y;
  Shot(float xx, float yy) {
    x=xx;
    y=yy;
  }
  boolean shotStuff() { //Moves the shot forward. Returns true if the shot is off the screen
    y=y-10;
    stroke(255);
    strokeWeight(4);
    line(x, y, x, y+10);
    if (y < -5) {
      return true;
    }
    return false;
  }
}
class Star {
  int x;
  int y;
  color col;
  int diam = int(random(1, 8));
  int blinkCounter = 0;
  Star(int xx, int yy) {
    col = color(random(0,255), random(0,255), random(0,255));
    x = xx;
    y = yy;
    fill(col);
    noStroke();
    ellipse(x, y, diam, diam);
  }
  boolean updateStar() { //Updates the star with possibility of blinking. Returns true if it is off the screen
    if (y > height) {
      return true;
    }
    y=y+1;
    int rand = int(random(0,500));
    if (rand <= 1) {
      blinkCounter = int(random(10, 50));
    }
    if (blink()) {
    }
    else {
      fill(col);
      noStroke();
      ellipse(x, y, diam, diam);
    }
    return false;
  }
  boolean blink() {
    if (blinkCounter == 0) {
      return false;
    }
    else {
      blinkCounter--;
      return true;
    }
  }
} 
    
		</script>
		<canvas> </canvas>
	</body>
</html>
